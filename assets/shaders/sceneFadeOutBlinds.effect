// Effect Syntax Guide: https://github.com/cocos-creator/docs-3d/blob/master/zh/material-system/effect-syntax.md

CCEffect %{
  techniques:
  - passes:
    - vert: sprite-vs:vert
      frag: sprite-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        blur_factor: { value: 0.0 }
        progress: { value: 0.0 }
        blink_level: { value: 0.0 }
}%

CCProgram sprite-vs %{
  precision highp float;

  #include <cc-global>

  #if USE_LOCAL
    #include <cc-local>
  #endif

  in vec3 a_position;
  in vec2 a_texCoord;
  in vec4 a_color;

  out vec4 color;
  out vec2 uv0;

  vec4 vert () {
    vec4 pos = vec4(a_position, 1);

    #if USE_LOCAL
      pos = cc_matWorld * pos;
    #endif

    #if USE_PIXEL_ALIGNMENT
      pos = cc_matView * pos;
      pos.xyz = floor(pos.xyz);
      pos = cc_matProj * pos;
    #else
      pos = cc_matViewProj * pos;
    #endif

    uv0 = a_texCoord;

    color = a_color;

    return pos;
  }
}%

CCProgram sprite-fs %{
  precision highp float;

  #include <embedded-alpha>
  #include <alpha-test>

  const float PI = 3.1415926;
  const float SLAT_WIDTH = 0.01; 
  const float SQRT_2 = sqrt(2.0);
  const vec2 BLUR_CENTER = vec2(.5, .5);
  const float MAX_SAMPLE_COUNT = 50.0;
  const float STRIDE = .006;
  const vec3 CAMERA_POSITION = vec3(.5, .5, 1.0);
  const vec3 CAMERA_DIRECTION = vec3(.0, .0, -1.0);

  uniform Constants {
    float progress;
    float blink_level;
    float blur_factor;
  };

  in vec4 color;

  float increaseBlurFactor(float blurFactor, vec2 uv) {
    return blurFactor * acos(dot(normalize(vec3(uv, 0.0) - CAMERA_POSITION), CAMERA_DIRECTION)) / PI;
  }

  vec4 getBlurColor(sampler2D spriteTexture, vec2 uv0) {
    if (blur_factor == 0.0) {
      return CCSampleTexture(spriteTexture, uv0);
    }
    
    vec2 direction = uv0 - BLUR_CENTER;
    vec2 sampleDirection = increaseBlurFactor(blur_factor, uv0) * direction;
    // vec2 sampleDirection = blur_factor * direction;
    float blurDistance = length(sampleDirection);
    float sample_count = blurDistance / STRIDE;

    uv0 -= sampleDirection / 2.0;

    vec4 out_color = vec4(0, 0, 0, 0);
    vec2 sample_stride_dir = sampleDirection / sample_count;
    vec2 uv = uv0;

    for (float i = 1.0; i < MAX_SAMPLE_COUNT; i += 1.0) {
      if (i >= sample_count) {
			  uv = uv0 + sampleDirection;

			  out_color += CCSampleTexture(spriteTexture, uv) * mod(blurDistance, STRIDE) / STRIDE;

        break;
      } else {
			  uv += sample_stride_dir;
        
			  out_color += CCSampleTexture(spriteTexture, uv);
      }
    }

    return out_color / sample_count;
  }

  #if USE_TEXTURE
    in vec2 uv0;
    #pragma builtin(local)
    layout(set = 2, binding = 10) uniform sampler2D cc_spriteTexture;
  #endif

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE
      if (progress < 1.0) {
        float t = uv0.t;

        if (mod(floor(t / SLAT_WIDTH), 2.0) == 0.0) {
          float s = uv0.s + progress;

          if (s > 1.0) {
            o = vec4(0, 0, 0, 1);
          } else {
            o *= getBlurColor(cc_spriteTexture, vec2(s, t));
          }
        } else {
          float s = uv0.s - progress;

          if (s < 0.0) {
            o = vec4(0, 0, 0, 1);
          } else {
            o *= getBlurColor(cc_spriteTexture, vec2(s, t));
          }
        }
      } else {
        o = vec4(0, 0, 0, 1);
      }
      #if IS_GRAY
        float gray  = 0.2126 * o.r + 0.7152 * o.g + 0.0722 * o.b;

        o.r = o.g = o.b = gray;
      #endif
    #endif

    if (blink_level > 0.0) {
      o = vec4(1, 1, 1, 1) * blink_level + o * (1.0 - blink_level);
    }

    o *= color;
    ALPHA_TEST(o);

    return o;
  }
}%
